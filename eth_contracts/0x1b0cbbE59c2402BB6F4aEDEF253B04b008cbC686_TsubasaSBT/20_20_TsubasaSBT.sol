// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Burnable.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/AccessControlEnumerable.sol";
import "@openzeppelin/contracts/utils/Context.sol";

/**
 * @dev {ERC721} token, including:
 *
 *  - cannot transfer this token to the other account(SBT)
 *  - a minter role that allows for token minting (creation)
 *  - token URI autogeneration
 *
 * This contract uses {AccessControl} to lock permissioned functions using the
 * different roles - head to its documentation for details.
 *
 * The account that deploys the contract will be granted the minter
 * as well as the default admin role, which will let it grant  minter
 * role to other accounts.
 */
contract TsubasaSBT is
    Context,
    AccessControlEnumerable,
    ERC721Enumerable,
    ERC721URIStorage
{
    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");

    /**
     * @notice timestamp of each minted nft. tokenId => timestamp
     */
    mapping(uint256 => uint256) public mintAt;

    address public owner;

    string private _baseTokenURI;

    event OwnershipTransferred(
        address indexed previousOwner,
        address indexed newOwner
    );

    modifier onlyAdmin() {
        require(
            hasRole(DEFAULT_ADMIN_ROLE, _msgSender()),
            "TsubasaSBT: Caller not allowed to operate"
        );
        _;
    }

    modifier onlyMinter() {
        require(
            hasRole(MINTER_ROLE, _msgSender()),
            "TsubasaSBT: Caller not allowed to operate"
        );
        _;
    }

    /**
     * @dev Grants `DEFAULT_ADMIN_ROLE`, `MINTER_ROLE` to the
     * account that deploys the contract.
     *
     * Token URIs will be autogenerated based on `baseURI` and their token IDs.
     * See {ERC721-tokenURI}.
     */
    constructor(
        address _owner,
        string memory name,
        string memory symbol,
        string memory baseTokenURI
    ) ERC721(name, symbol) {
        _baseTokenURI = baseTokenURI;

        _setupRole(DEFAULT_ADMIN_ROLE, _owner);
        _setupRole(MINTER_ROLE, _owner);
        _transferOwnership(_owner);
    }

    function _baseURI() internal view virtual override returns (string memory) {
        return _baseTokenURI;
    }

    /**
     * @dev Creates a new token for `to`. Its token ID will be automatically
     * assigned (and available on the emitted {IERC721-Transfer} event), and the token
     * URI autogenerated based on the base URI passed at construction.
     *
     * See {ERC721-_mint}.
     *
     * Requirements:
     *
     * - the caller must have the `MINTER_ROLE`.
     */

    function mint(address to, uint256 tokenId) public onlyMinter {
        _mint(to, tokenId);
    }

    /**
     * setBaseURI
     * @param baseURI base URI of NFT.
     */
    function setBaseURI(string memory baseURI) public onlyAdmin {
        _baseTokenURI = baseURI;
    }

    /**
     * setTokenURI
     * @param tokenId NFT token ID.
     * @param _tokenURI token URI of NFT.
     */
    function setTokenURI(
        uint256 tokenId,
        string memory _tokenURI
    ) public onlyMinter {
        _setTokenURI(tokenId, _tokenURI);
    }

    /**
     * Bulk setTokenURI
     * @param tokenIds List of tokenId.
     * @param tokenURIs List of tokenURI.
     */
    function setTokenURI(
        uint256[] memory tokenIds,
        string[] memory tokenURIs
    ) public onlyMinter {
        require(
            tokenIds.length == tokenURIs.length,
            "SBT: args must be equals"
        );
        for (uint256 i; i < tokenIds.length; i++) {
            _setTokenURI(tokenIds[i], tokenURIs[i]);
        }
    }

    /**
     * @notice set minter role. only admin can call this.
     */
    function setMinterRole(address _to) external onlyAdmin {
        _grantRole(MINTER_ROLE, _to);
    }

    /**
     * @notice remove minter role from the specified accout. only admin can call this.
     */
    function removeMinterRole(address _from) external onlyAdmin {
        _revokeRole(MINTER_ROLE, _from);
    }

    /**
     * Transfer Ownership
     * @param newOwner Address of new owner.
     */
    function transferOwnership(address newOwner) public onlyAdmin {
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal {
        address oldOwner = owner;
        owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }

    function tokenURI(
        uint256 tokenId
    )
        public
        view
        virtual
        override(ERC721, ERC721URIStorage)
        returns (string memory)
    {
        return super.tokenURI(tokenId);
    }

    function _burn(
        uint256 tokenId
    ) internal override(ERC721, ERC721URIStorage) {
        super._burn(tokenId);
    }

    /**
     * @notice not allow users to transfer except for zero address.
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId,
        uint256 batchSize
    ) internal override(ERC721, ERC721Enumerable) {
        require(
            from == address(0),
            "TsubasaSBT: this token is not transferable"
        );
        super._beforeTokenTransfer(from, to, tokenId, batchSize);
    }

    /**
     * @dev overide ERC721._mint to record timestamp
     */
    function _mint(address _to, uint256 _tokenId) internal override {
        mintAt[_tokenId] = block.timestamp;
        super._mint(_to, _tokenId);
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(
        bytes4 interfaceId
    )
        public
        view
        override(AccessControlEnumerable, ERC721, ERC721Enumerable)
        returns (bool)
    {
        return super.supportsInterface(interfaceId);
    }
}