// SPDX-License-Identifier: MIT

// Developed by:
//////////////////////////////////////////////solarprotocol.io//////////////////////////////////////////
//_____/\\\\\\\\\\\_________/\\\\\_______/\\\__0xFluffyBeard__/\\\\\\\\\_______/\\\\\\\\\_____        //
// ___/\\\/////////\\\_____/\\\///\\\____\/\\\_______________/\\\\\\\\\\\\\___/\\\///////\\\___       //
//  __\//\\\______\///____/\\\/__\///\\\__\/\\\______________/\\\/////////\\\_\/\\\_____\/\\\___      //
//   ___\////\\\__________/\\\______\//\\\_\/\\\_____________\/\\\_______\/\\\_\/\\\\\\\\\\\/____     //
//    ______\////\\\______\/\\\_______\/\\\_\/\\\_____________\/\\\\\\\\\\\\\\\_\/\\\//////\\\____    //
//     _________\////\\\___\//\\\______/\\\__\/\\\_____________\/\\\/////////\\\_\/\\\____\//\\\___   //
//      __/\\\______\//\\\___\///\\\__/\\\____\/\\\_____________\/\\\_______\/\\\_\/\\\_____\//\\\__  //
//       _\///\\\\\\\\\\\/______\///\\\\\/_____\/\\\\\\\\\\\\\\\_\/\\\_______\/\\\_\/\\\______\//\\\_ //
//        ___\///////////__________\/////_______\///////////////__\///________\///__\///________\///__//
////////////////////////////////////////////////////////////////////////////////////////////////////////

pragma solidity ^0.8.9;

import {LibContext} from "../libraries/LibContext.sol";
import {LibUtils} from "../libraries/LibUtils.sol";

/**
 * @dev Library version of the OpenZeppelin Pausable contract with Diamond storage.
 * See: https://docs.openzeppelin.com/contracts/4.x/api/security#Pausable
 * See: https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/security/Pausable.sol
 */
library LibPausable {
    struct Storage {
        bool paused;
        mapping(address => bool) exemptFromPause;
    }

    bytes32 private constant STORAGE_SLOT =
        keccak256("solarprotocol.contracts.pausable.LibPausable");

    /**
     * @dev Returns the storage.
     */
    function _storage() private pure returns (Storage storage s) {
        bytes32 slot = STORAGE_SLOT;
        // solhint-disable-next-line no-inline-assembly
        assembly {
            s.slot := slot
        }
    }

    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    /**
     * @dev Emitted when the exempt from pause status of `account` is updated.
     */
    event UpdateExemptFromPause(address account, bool flag);

    /**
     * @dev Reverts when paused.
     */
    function enforceNotPaused() internal view {
        require(
            !paused() ||
                LibUtils.isOwner() ||
                _storage().exemptFromPause[LibContext.msgSender()],
            "Pausable: paused"
        );
    }

    /**
     * @dev Reverts when not paused.
     */
    function enforcePaused() internal view {
        require(
            paused() ||
                LibUtils.isOwner() ||
                _storage().exemptFromPause[LibContext.msgSender()],
            "Pausable: not paused"
        );
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() internal view returns (bool) {
        return _storage().paused;
    }

    /**
     * @dev Triggers stopped state.
     */
    function pause() internal {
        _storage().paused = true;
        emit Paused(LibContext.msgSender());
    }

    /**
     * @dev Returns to normal state.
     */
    function unpause() internal {
        _storage().paused = false;
        emit Unpaused(LibContext.msgSender());
    }

    /**
     * @dev Returns true if `account` is exempt from the pause.
     */
    function isExemptFromPause(address account) internal view returns (bool) {
        return _storage().exemptFromPause[account];
    }

    /**
     * Updates the exempt from pause state of `account` to `flag`.
     */
    function setExemptFromPause(address account, bool flag) internal {
        _storage().exemptFromPause[account] = flag;

        emit UpdateExemptFromPause(account, flag);
    }
}