// SPDX-License-Identifier: Unlicensed

/**

Telegram Portal: https://t.me/ShibaDoge_Portal
Website: https://deflationary.com / https://burn.realshibadoge.com / https://realshibadoge.com
Twitter: https://twitter.com/RealShibaDoge
Medium: https://realshibadoge.medium.com
Discord: https://discord.gg/realshibadoge

                        MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
                        MMMMMMMMMMMMMMMMMMMMMMMMMMMMWWNXK0OOkkOOOOO00KXNWWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
                        MMMMMMMMMMMMMMMMMMMMMMMWN0kdlc:,,,'',;;;;;;;;;:cloxk0XWMMMMMMMMMMMMMMMMMMMMMMMMM
                        MMMMMMMMMMMMMMMMMMMWX0dl;,',,;::cclllodooooooollc::;;:lxOXWMMMMMMMMMMMMMMMMMMMMM
                        MMMMMMMMMMMMMMMMWXko;'',:clloooooooooodddddddddddddolc:;;:okKWMMMMMMMMMMMMMMMMMM
                        MMMMMMMMMMMMMMN0o;'';cloooooooooooooocclodddddddddddddddoc;;:oONWMMMMMMMMMMMMMMM
                        MMMMMMMMMMMMNOc,';cloooooooooooooooool;;:coddddddddddddddddoc;;ckXWMMMMMMMMMMMMM
                        MMMMMMMMMMNOc'';cooooooooooooooooolloo:,,,:oddddddddddddddddddc;,ckXWMMMMMMMMMMM
                        MMMMMMMMWKo'';looooooooooooooooool:;lo:;,,,:ldddddddddddddddddddc;,l0WMMMMMMMMMM
                        MMMMMMMNk;.,coooooooooooooooooool:,,cl:,,,,,:odddooddddddddddddddo:,;xXMMMMMMMMM
                        MMMMMMNd,.;loooooooooooooooooool:,,;cc;,,,,,;cdddl:coddddddddddddddl,,oKWMMMMMMM
                        MMMMMXo'':looooooooooooooooooool:,;:l:,,,,,,,codol;,:lddddddddddddddl;'lKWMMMMMM
                        MMMMNd'':oooooooooooooooooooolclc:cl:,,,,,,,;:c:cl:,,,codddddddddddddl;'lKMMMMMM
                        MMMWx,.:oooooooooooooooooool:,;looc;,,,,,,,,,;,,;ll;,,,cdddddddddddoddl,'oXMMMMM
                        MMM0:.;looooooooooooooooooc;',:ll:,,,,,,,,,,,,,,,;cl:;,codddddddddooodoc',kWMMMM
                        MMNd.'cooooooooooooooooool;',,;;,,,,,,,,,,,,,,,,,,;cllclodddddddoooooooo;.cKMMMM
                        MMK:.;ooooooooooooooooool:,,,,,,,,,,,,,,,,,,,,,,,,,,:lddoddddddoooooooooc.,kWMMM
                        MWk,.:oooooooooooooooolll:,,,,,,,,,,,,,,,,,,,,,,,,,,,;colcoddddoooooooool,.oNMMM
                        MWd''coooooooooooooooc;:l:;,,;;;;;;;;;;:::::::::::;;,,,::,:odoooooooooool,.cXMMM
                        MNd.'cooooooooooooooc;',;:;,;cloooooooooooooddddoool:;,,,,,:ooooooooooooo;.cKMMM
                        MNd.'coooooooooooool;,,,,,,,:oddddddddddddddddddddddo:,,,,,;coooooooooooo;.cKMMM
                        MWx''cooooooooooool:,,,,,,,;coddddddddddddddddddddddoc;,,,,,:oooooooooool,.lXMMM
                        MWO;.:ooooooooooool;,,,,,,,;cddddddddo:;;;;codddddddo:;,,,,,:looooooooooc''dWMMM
                        MMXl.,loooooooooooc;,,,,,,,;lddddddddl;,,,,:oddddddoc;,,,,,,;loooooooooo:.;0WMMM
                        MMWk,':oooooooooooc;,,,,,,,:oddddddddlcccclodddddol:,,,,,,,,;loooooooool'.oNMMMM
                        MMMXl.,cooooooddddc;,,,,,,;coddddddddddddddddddol:,,,,,,,,,,:looooooooo;.:0WMMMM
                        MMMW0:.,loodddddddl;,,,,,,;ldddddddddddddddddddoolc:;,,,,,,;cloooooooo:.,kWMMMMM
                        MMMMWO:':oddddddddoc;,,,,,:ldddddddddoooooodddddoool:;,,,,,:loooooooo:.'xNMMMMMM
                        MMMMMW0c,:oddddddddl:;,,,,:odddddddoc;,,,,;lddooooooc;,,,,;coooooool;.,xNMMMMMMM
                        MMMMMMWKo,;lddddddddoc;,,;codddddddo:,,,,,;looooooool;,,,:coooooool,.;kNMMMMMMMM
                        MMMMMMMMXx:,:odddddddol:;;lddddddddoc::cccloooooooolc;,;:looooool:''l0WMMMMMMMMM
                        MMMMMMMMMW0o;;codddddddolcoddddddddddddddddooooooooc;;:cloooool:'.;kNMMMMMMMMMMM
                        MMMMMMMMMMMNOl;;codddddddddddddddddddddddoooooooolc::lloooool:'.;dXWMMMMMMMMMMMM
                        MMMMMMMMMMMMMN0o;,:coddddddddddddddddddoooooooooolloooooolc;'':xXWMMMMMMMMMMMMMM
                        MMMMMMMMMMMMMMMWKxc;;:clodddddddddddddooooooooooooooolc:;'';oONMMMMMMMMMMMMMMMMM
                        MMMMMMMMMMMMMMMMMWN0xl:,;:ccloodddddddoooooooooolcc:;'',:dOXWMMMMMMMMMMMMMMMMMMM
                        MMMMMMMMMMMMMMMMMMMMMWXOxl:;,,;;;:::::::::::;;,,'',:ldkKNMMMMMMMMMMMMMMMMMMMMMMM
                        MMMMMMMMMMMMMMMMMMMMMMMMMWNK0kxolcc::;;;;::ccloxk0KNWMMMMMMMMMMMMMMMMMMMMMMMMMMM
                        MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWWNNNNNNNNNWWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
                        MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM

    88888888ba   88        88  88888888ba   888b      88     888888888888  ,ad8888ba,    88      a8P   88888888888  888b      88
    88      "8b  88        88  88      "8b  8888b     88          88      d8"'    `"8b   88    ,88'    88           8888b     88
    88      ,8P  88        88  88      ,8P  88 `8b    88          88     d8'        `8b  88  ,88"      88           88 `8b    88
    88aaaaaa8P'  88        88  88aaaaaa8P'  88  `8b   88          88     88          88  88,d88'       88aaaaa      88  `8b   88
    88""""""8b,  88        88  88""""88'    88   `8b  88          88     88          88  8888"88,      88"""""      88   `8b  88
    88      `8b  88        88  88    `8b    88    `8b 88          88     Y8,        ,8P  88P   Y8b     88           88    `8b 88
    88      a8P  Y8a.    .a8P  88     `8b   88     `8888          88      Y8a.    .a8P   88     "88,   88           88     `8888
    88888888P"    `"Y8888Y"'   88      `8b  88      `888          88       `"Y8888Y"'    88       Y8b  88888888888  88      `888

 */

pragma solidity 0.8.13;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol";

import "@uniswap/v2-core/contracts/interfaces/IUniswapV2Factory.sol";
import "@uniswap/v2-core/contracts/interfaces/IUniswapV2Pair.sol";

import "@uniswap/v2-periphery/contracts/interfaces/IUniswapV2Router02.sol";

contract BurnTokenX is ERC20Burnable, Ownable {
    uint256 constant _initial_supply = 1 * (10**12) * (10**18); // 1 trillion tokens, 18 decimals

    uint256 public tokensBurned;

    address payable public _treasuryWallet;
    address payable public _developmentWallet;
    address payable public _buybackWallet;

    uint256 public maxWalletSize = (_initial_supply * 15) / 10000; // .15% of totalsupply
    uint256 public maxTransactionSize = (_initial_supply * 15) / 100000; // .015% of totalsupply
    uint256 public swapThreshold = (_initial_supply * 135) / 1000000; // .0135% of totalsupply

    IUniswapV2Router02 public immutable uniswapV2Router;
    address public immutable uniswapV2Pair;

    bool inSwapAndLiquify;
    bool public swapAndLiquifyEnabled = true;

    uint256 liquidityLockTime = 0;

    mapping(address => bool) public _isBlackListedBot;

    mapping(address => bool) public _isExcludedFromFee;
    mapping(address => bool) public _isExcludedFromMaxTransaction;
    mapping(address => bool) public _isExcludedFromMaxWallet;

    enum FeeType {
        None,
        Buy,
        Sell
    }

    struct BuyFee {
        uint16 liquidity;
        uint16 treasury;
        uint16 dev;
        uint16 buyback;
    }

    struct SellFee {
        uint16 liquidity;
        uint16 treasury;
        uint16 dev;
        uint16 buyback;
    }

    BuyFee public buyFee;
    SellFee public sellFee;

    uint256 constant FEE_DENOM = 1000; // smallest fee unit is 0.1%

    event botAddedToBlacklist(address account);
    event botRemovedFromBlacklist(address account);

    event excludedFromFee(address account);
    event excludedFromMaxTransaction(address account);
    event excludedFromMaxWallet(address account);

    event includedInFee(address account);
    event includedInMaxTransaction(address account);
    event includedInMaxWallet(address account);

    event treasuryWalletUpdated(address treasuryWallet);
    event developmentWalletUpdated(address developmentWallet);
    event buybackWalletUpdated(address buybackWallet);

    event liquidityRemoved(uint256 amountToken, uint256 amountETH);

    event swapThresholdUpdated(uint256 minTokensBeforeSwap);
    event SwapAndLiquifyEnabledUpdated(bool enabled);
    event SwapAndLiquify(
        uint256 tokensSwapped,
        uint256 ethReceived,
        uint256 tokensIntoLiquidity
    );

    event MaxWalletSizeUpdated(uint256 maxWalletSize);
    event MaxTransactionSizeUpdated(uint256 maxTransactionSize);

    event LiquidityLockTimeUpdated(uint256 lockTime);

    modifier lockTheSwap() {
        inSwapAndLiquify = true;
        _;
        inSwapAndLiquify = false;
    }

    constructor(
        string memory name__,
        string memory symbol__,
        address treasury,
        address development,
        address buyback,
        address[8] memory teamWallets
    ) ERC20(name__, symbol__) {
        // set fees to max
        setSellFee(470, 0, 480, 0);
        setBuyFee(470, 0, 480, 0);

        require(treasury != address(0), "Treasury wallet cannot be 0x0");
        require(development != address(0), "Development wallet cannot be 0x0");
        require(buyback != address(0), "Buyback wallet cannot be 0x0");

        _treasuryWallet = payable(treasury);
        _developmentWallet = payable(development);
        _buybackWallet = payable(buyback);

        IUniswapV2Router02 _uniswapV2Router = IUniswapV2Router02(
            0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D
        );

        // Create a uniswap pair for this new token
        uniswapV2Pair = IUniswapV2Factory(_uniswapV2Router.factory())
            .createPair(address(this), _uniswapV2Router.WETH());

        // set the rest of the contract variables
        uniswapV2Router = _uniswapV2Router;

        _mint(msg.sender, (_initial_supply * 14) / 100); // 14% for deployer
        _mint(msg.sender, (_initial_supply * 5) / 100); // for liquidity pool

        _mint(teamWallets[0], (_initial_supply * 15) / 10000); // .15% for team 1
        _mint(teamWallets[1], (_initial_supply * 15) / 10000); // .15% for team 2
        _mint(teamWallets[2], (_initial_supply * 15) / 10000); // .15% for team 3
        _mint(teamWallets[3], (_initial_supply * 15) / 10000); // .15% for team 4
        _mint(teamWallets[4], (_initial_supply * 10) / 10000); // .1% for team 5
        _mint(teamWallets[5], (_initial_supply * 10) / 10000); // .1% for team 6
        _mint(teamWallets[6], (_initial_supply * 10) / 10000); // .1% for team 7
        _mint(teamWallets[7], (_initial_supply * 10) / 10000); // .1% for team 8

        _mint(
            0x000000000000000000000000000000000000dEaD,
            (_initial_supply * 80) / 100
        ); // burn baby burnnnnn it's a crypto inferno

        _isExcludedFromFee[msg.sender] = true;
        _isExcludedFromMaxTransaction[msg.sender] = true;
        _isExcludedFromMaxWallet[msg.sender] = true;

        _isExcludedFromFee[address(this)] = true;
        _isExcludedFromMaxTransaction[address(this)] = true;
        _isExcludedFromMaxWallet[address(this)] = true;

        _isExcludedFromFee[_treasuryWallet] = true;
        _isExcludedFromMaxTransaction[_treasuryWallet] = true;
        _isExcludedFromMaxWallet[_treasuryWallet] = true;

        _isExcludedFromFee[_developmentWallet] = true;
        _isExcludedFromMaxTransaction[_developmentWallet] = true;
        _isExcludedFromMaxWallet[_developmentWallet] = true;

        _isExcludedFromFee[_buybackWallet] = true;
        _isExcludedFromMaxTransaction[_buybackWallet] = true;
        _isExcludedFromMaxWallet[_buybackWallet] = true;
    }

    //to receive ETH from uniswapV2Router when swapping
    receive() external payable {}

    // public functions

    function transfer(address to, uint256 amount)
        public
        override
        returns (bool)
    {
        address _owner = _msgSender();
        (bool takeFee, FeeType feeType) = checkFeeRequired(_owner, to);

        checkTransferAllowed(_owner, to, amount, takeFee);

        if (takeFee) {
            //check for swapAndLiquify available
            uint256 contractBalance = balanceOf(address(this));
            if (
                contractBalance >= swapThreshold &&
                !inSwapAndLiquify &&
                swapAndLiquifyEnabled &&
                _owner != uniswapV2Pair
            ) {
                //perform swapAndLiquify
                swapAndLiquify(swapThreshold);
            }

            uint256 fee = calculateFee(amount, feeType);
            _transfer(_owner, address(this), fee);
            _transfer(_owner, to, amount - fee);
        } else {
            _transfer(_owner, to, amount);
        }

        return true;
    }

    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) public override returns (bool) {
        address spender = _msgSender();

        (bool takeFee, FeeType feeType) = checkFeeRequired(from, to);
        checkTransferAllowed(from, to, amount, takeFee);

        _spendAllowance(from, spender, amount);

        if (takeFee) {
            //check for swapAndLiquify available
            uint256 contractBalance = balanceOf(address(this));
            if (
                contractBalance >= swapThreshold &&
                !inSwapAndLiquify &&
                swapAndLiquifyEnabled &&
                from != uniswapV2Pair
            ) {
                //perform swapAndLiquify
                swapAndLiquify(swapThreshold);
            }

            uint256 fee = calculateFee(amount, feeType);
            _transfer(from, address(this), fee);
            _transfer(from, to, amount - fee);
        } else {
            _transfer(from, to, amount);
        }

        return true;
    }

    function burn(uint256 amount) public override {
        _burn(_msgSender(), amount);
        tokensBurned += amount;
    }

    function burnFrom(address account, uint256 amount) public override {
        _spendAllowance(account, _msgSender(), amount);
        _burn(account, amount);
        tokensBurned += amount;
    }

    function liquidityLockedUntil() external view returns (uint256) {
        return liquidityLockTime;
    }

    // internal functions

    function checkTransferAllowed(
        address from,
        address to,
        uint256 amount,
        bool takeFee
    ) internal view {
        require(
            !_isBlackListedBot[from] || !_isBlackListedBot[to],
            "You are blacklisted"
        );
        if (to != uniswapV2Pair) {
            require(
                balanceOf(to) + amount < maxWalletSize ||
                    _isExcludedFromMaxWallet[to],
                "Exceeds receivers maximum wallet size"
            );
        }
        if (takeFee) {
            require(
                amount <= maxTransactionSize ||
                    (_isExcludedFromMaxTransaction[from] ||
                        _isExcludedFromMaxTransaction[to]),
                "Transaction larger than allowed"
            );
        }
    }

    function checkFeeRequired(address from, address to)
        internal
        view
        returns (bool, FeeType)
    {
        if (from == uniswapV2Pair && !_isExcludedFromFee[to]) {
            return (true, FeeType.Buy);
        } else if (to == uniswapV2Pair && !_isExcludedFromFee[from]) {
            return (true, FeeType.Sell);
        } else {
            return (false, FeeType.None);
        }
    }

    function calculateFee(uint256 amount, FeeType feeType)
        internal
        view
        returns (uint256 fee)
    {
        uint256 feePercentage = 0;
        if (feeType == FeeType.Buy) {
            feePercentage =
                buyFee.treasury +
                buyFee.liquidity +
                buyFee.dev +
                buyFee.buyback;
        } else if (feeType == FeeType.Sell) {
            feePercentage =
                sellFee.treasury +
                sellFee.liquidity +
                sellFee.dev +
                sellFee.buyback;
        }
        fee = (amount * feePercentage) / FEE_DENOM;

        return fee;
    }

    function swapAndLiquify(uint256 tokens) internal lockTheSwap {
        // split tokens by buy fee ratio
        uint256 feeDenominator = buyFee.treasury +
            buyFee.liquidity +
            buyFee.dev +
            buyFee.buyback;

        uint256 liquidityFee = (tokens * buyFee.liquidity) / feeDenominator;

        // sell tokens minus half of liquidity cut for eth
        swapTokensForEth(tokens - (liquidityFee / 2));

        // split resulting eth balance of contract by ratio, giving liquidity half weight
        uint256 contractEth = address(this).balance;

        uint256 ethDenominator = buyFee.treasury +
            (buyFee.liquidity / 2) +
            buyFee.dev +
            buyFee.buyback;

        uint256 treasuryEth = (contractEth * buyFee.treasury) / ethDenominator;
        uint256 liquidityEth = (contractEth * (buyFee.liquidity / 2)) /
            ethDenominator;
        uint256 buybackEth = (contractEth * buyFee.buyback) / ethDenominator;

        // provide liquidity with eth portion and remaining tokens

        if (liquidityEth > 0) {
            // Add liquidity to uniswap
            addLiquidity(liquidityFee / 2, liquidityEth);
        }

        _treasuryWallet.transfer(treasuryEth);

        _buybackWallet.transfer(buybackEth);

        // send all remaining eth to dev wallet (in case of rounding)
        _developmentWallet.transfer(address(this).balance); 

        emit SwapAndLiquify(tokens, contractEth, liquidityFee / 2);
    }

    function swapTokensForEth(uint256 tokenAmount) internal {
        // generate the uniswap pair path of token -> weth
        address[] memory path = new address[](2);
        path[0] = address(this);
        path[1] = uniswapV2Router.WETH();

        _approve(address(this), address(uniswapV2Router), tokenAmount);

        // make the swap
        uniswapV2Router.swapExactTokensForETHSupportingFeeOnTransferTokens(
            tokenAmount,
            0, // accept any amount of ETH
            path,
            address(this),
            block.timestamp
        );
    }

    function addLiquidity(uint256 tokenAmount, uint256 ethAmount) internal {
        // approve token transfer to cover all possible scenarios
        _approve(address(this), address(uniswapV2Router), tokenAmount);

        // add the liquidity
        uniswapV2Router.addLiquidityETH{value: ethAmount}(
            address(this),
            tokenAmount,
            0, // slippage is unavoidable
            0, // slippage is unavoidable
            address(this),
            block.timestamp
        );
    }

    // admin functions

    function setTreasuryWallet(address treasury) external onlyOwner {
        require(treasury != address(0), "Treasury wallet cannot be 0x0");
        _treasuryWallet = payable(treasury);
        emit treasuryWalletUpdated(_treasuryWallet);
    }

    function setDevelopmentWallet(address development) external onlyOwner {
        require(development != address(0), "Development wallet cannot be 0x0");
        _developmentWallet = payable(development);
        emit developmentWalletUpdated(_developmentWallet);
    }

    function setBuybackWallet(address buyback) external onlyOwner {
        require(buyback != address(0), "Buyback wallet cannot be 0x0");
        _buybackWallet = payable(buyback);
        emit buybackWalletUpdated(_treasuryWallet);
    }

    function setSellFee(
        uint16 liquidity,
        uint16 treasury,
        uint16 dev,
        uint16 buyback
    ) public onlyOwner {
        require(
            liquidity + treasury + dev + buyback < FEE_DENOM,
            "invalid fee structure"
        );

        sellFee.treasury = treasury;
        sellFee.liquidity = liquidity;
        sellFee.dev = dev;
        sellFee.buyback = buyback;
    }

    function setBuyFee(
        uint16 liquidity,
        uint16 treasury,
        uint16 dev,
        uint16 buyback
    ) public onlyOwner {
        require(
            liquidity + treasury + dev + buyback < FEE_DENOM,
            "invalid fee structure"
        );

        buyFee.treasury = treasury;
        buyFee.liquidity = liquidity;
        buyFee.dev = dev;
        buyFee.buyback = buyback;
    }

    function setBothFees(
        uint16 buy_liquidity,
        uint16 buy_treasury,
        uint16 buy_dev,
        uint16 buy_buyback,
        uint16 sell_liquidity,
        uint16 sell_treasury,
        uint16 sell_dev,
        uint16 sell_buyback
    ) external onlyOwner {
        setBuyFee(buy_liquidity, buy_treasury, buy_dev, buy_buyback);

        setSellFee(sell_liquidity, sell_treasury, sell_dev, sell_buyback);
    }

    function setSwapThreshold(uint256 newSwapThreshold) external onlyOwner {
        swapThreshold = newSwapThreshold;
        emit swapThresholdUpdated(newSwapThreshold);
    }

    function setMaxTransactionSize(uint256 maxTxSize) external onlyOwner {
        maxTransactionSize = maxTxSize;
        emit MaxTransactionSizeUpdated(maxTransactionSize);
    }

    function setMaxWalletSize(uint256 maxWallet) external onlyOwner {
        maxWalletSize = maxWallet;
        emit MaxWalletSizeUpdated(maxWalletSize);
    }

    function addBotToBlacklist(address account) external onlyOwner {
        require(!_isBlackListedBot[account], "Account is already blacklisted");
        _isBlackListedBot[account] = true;

        emit botAddedToBlacklist(account);
    }

    function removeBotFromBlacklist(address account) external onlyOwner {
        require(_isBlackListedBot[account], "Account is not blacklisted");
        _isBlackListedBot[account] = false;

        emit botRemovedFromBlacklist(account);
    }

    function excludeFromFee(address account) external onlyOwner {
        require(
            !_isExcludedFromFee[account],
            "Account is already excluded from fee"
        );
        _isExcludedFromFee[account] = true;

        emit excludedFromFee(account);
    }

    function excludeFromMaxTransaction(address account) external onlyOwner {
        require(
            !_isExcludedFromMaxTransaction[account],
            "Account is already excluded from max transaction"
        );
        _isExcludedFromMaxTransaction[account] = true;

        emit excludedFromMaxTransaction(account);
    }

    function excludeFromMaxWallet(address account) external onlyOwner {
        require(
            !_isExcludedFromMaxWallet[account],
            "Account is already excluded from max wallet"
        );
        _isExcludedFromMaxWallet[account] = true;

        emit excludedFromMaxWallet(account);
    }

    function includeInFee(address account) external onlyOwner {
        require(
            _isExcludedFromFee[account],
            "Account is already included in fee"
        );
        _isExcludedFromFee[account] = false;

        emit includedInFee(account);
    }

    function includeInMaxTransaction(address account) external onlyOwner {
        require(
            _isExcludedFromMaxTransaction[account],
            "Account is already included in max transaction"
        );
        _isExcludedFromMaxTransaction[account] = false;

        emit includedInMaxTransaction(account);
    }

    function includeInMaxWallet(address account) external onlyOwner {
        require(
            _isExcludedFromMaxWallet[account],
            "Account is already included in max wallet"
        );
        _isExcludedFromMaxWallet[account] = false;

        emit includedInMaxWallet(account);
    }

    function updateLiquidityLock(uint256 lockTime) external onlyOwner {
        require(
            block.timestamp > liquidityLockTime,
            "New liquidity lock time must be after the current lock time"
        );
        liquidityLockTime = lockTime;
        emit LiquidityLockTimeUpdated(lockTime);
    }

    // wrapper for Uniswap removeLiquidity, can only be called if not locked
    function removeLiquidityETH(
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountETHMin,
        address to,
        uint256 deadline
    ) external onlyOwner {
        require(
            block.timestamp > liquidityLockTime,
            "Liquidity removal is locked"
        );

        (uint256 amountToken, uint256 amountETH) = uniswapV2Router
            .removeLiquidityETH(
                address(this),
                liquidity,
                amountTokenMin,
                amountETHMin,
                to,
                deadline
            );

        emit liquidityRemoved(amountToken, amountETH);
    }

    function toggleSwapAndLiquifyEnabled() external onlyOwner {
        swapAndLiquifyEnabled = !swapAndLiquifyEnabled;
        emit SwapAndLiquifyEnabledUpdated(swapAndLiquifyEnabled);
    }

    function adminSwapAndLiquify(uint256 swapBalance) external onlyOwner {
        swapAndLiquify(swapBalance);
    }

    // Recovery functions

    function rescueEth() external payable onlyOwner {
        payable(msg.sender).transfer(address(this).balance);
    }

    function rescueTokens(address _stuckToken, uint256 _amount)
        external
        onlyOwner
    {
        IERC20(_stuckToken).transfer(msg.sender, _amount);
    }

    function rescueBurn(uint256 _amount) external onlyOwner {
        _transfer(address(this), msg.sender, _amount);
    }
}