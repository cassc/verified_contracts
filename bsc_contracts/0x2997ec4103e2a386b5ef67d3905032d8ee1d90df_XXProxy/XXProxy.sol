/**
 *Submitted for verification at BscScan.com on 2023-02-06
*/

// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMBuED~(zET]!b4nRMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWdE1>uAk:__{n6aMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM#xP{j3z)_**~kPeMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// [email protected]_**>|zscMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// [email protected]<<<>/kEzXMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMd6a99{><>!bE9wMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%%MMMhn4KI_>/U6XqmMMM%MMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMBNp6A60JUp34qLP#[email protected]
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMEKBdbH4A43458554X36v6HSMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%MMMB9pz3EK9edfuV2222Cyq6EXdde$MMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMBXHFEHtO^](|:<_~~~~~</[JfP3xX8%MMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%M569EKqQ]>>__>><<******<>__<[U63o3MMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNg64dJ;~><<<<<<<<<<<<<<<<<<<>_^d3Ks8MMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMN3XE9O;_><<<<<<<<<<<<<<<<<<<<<<<>:InXxRMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWfpAb}_<<<<<<<<<<<<<<<<<<<<<<<<<<<>>}b3xRM%MMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMpzAv|_<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_/c3xRMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNPEG:><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<*>/o3z8MMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM5p3v*><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>)q3fRM%%MMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMoFz)><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_{nXAM%MMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%3HXl_*<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_jXxXM%MMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%M4kAu<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<GKH%MMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM$aH9[><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>)[email protected]
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM8bAI_<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_OEp8MMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMt69]><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>|bXpMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%63u<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_L3aKMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%M6zKl_<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<:q9XMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMME6p:<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_QEF&MMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMmc4Z_<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>[6xAMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMSfEJ><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>*f3u#MMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM&96(_<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_0Kg%MMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMSEb;><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_{9XwMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM$vKG<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>/zxfMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM$b32_<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<*oHzMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%sK7~<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>__>>>><_vX6MMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%39!_<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>><;[}[{}|<~CK4MMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMme}><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>/DfsPFPFPnu!DPBMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM3Xe(><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>!b4Ksqcccb6AXgKHmMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMM%Mfzg;><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<094kr[;***;}OxASknM%MMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMM%Moeq:<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>*IXXC*~_><>>>__(kAgEM%MMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMM%Moeq:<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>;ZE6L><<<<<<<<<<>:kEF%MMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMM%Moep:<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>;kAn1><<<<<<<<<<<<>(zPSMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMM%Moep:<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>/kAxr><<<<<<<<<<<<<*_VXoMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMM%Moep:<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<*YAsr>*<<<<<<<<<<<<<*~OPpMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMM%Moep:<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>IEnr_<<<<<<<<<<<<<<<*~CKpMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMM%Mdxq:<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_JXPj_<<<<<<<<<<<<><<<<<hXoMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMM%Mtap*<<<<<<<<<<<<<<<<<<<<<<<<<<<<>{sED><<<<<<<<<<*UI<<<<_lP68MMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMKXp/><<<<<<<<<<<<<<<<<<<<<<<<<<>|o5G<<<<<<<<<<<_lEo:_>_1nKH$MMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMwa[><<<<<<<<<<<<<<<<<<<<<<<<<<<VAq/><<<<<<<<<>(zKJ<|ju645$MMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%we[><<<<<<<<<<<<<<<<<<<<<<<<<_rF6i_*<<<<<<<<<>VA6Yva336KNMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%3n!_<<<<<<<<<<<<<<<<<<<<<<<<>/dAU><<<<<<<<<<_{H4ESA9o0OX65M%MMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%6Hr_<<<<<<<<<<<<<<<<<<<<<><<_24g(><<<<<<<<<<*hAvOgU(:>uAXbSMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%H6r_<<<<<<<<<<<<<<<<<<<<<<<>(e3l_<<<<<<<<<<_1Ka/>)*__LXHPEc4MMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM$eFj_*<<<<<<<<<<<<<<<<<<<<<<>TAh<<<<<<<<<<<>:c30_*><>^s3vuXFvRMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM$b3L~*<<<<<<<<<<<<<<<<<<<<<_[9Ki_<<<<<<<<<<_7Eq;<<<<*fAoQQuXHEMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM$b3L~*<<<<<<<<<<<<<<<<<<<<<_Y5k*<<<<<<<<<<>;b4L><<<>2EsCOQOh4xKMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM$d30~*<<<<<<<<<<<<<<<<<<<<>(eK1_<<<<<<<<<<_LEb;><<>rHFYOQQO2qKF%MMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM$kKI_<<<<<<<<<<<<<<<<<<<<<_04d:<<<<<<<<<<<*dEL_<<>|bEhQQQQQQIsXa#MMMM
// [email protected]_<<<<<<<<<<<<<<<<<<<<>:c4L_<<<<<<<<<<_1Kg;><<<V4zIQQ2QQODf4GmMMMM
// [email protected]><<<<<<<<<<<<<<<<<<<<_!Fa/><<<<<<<<<<:f40_<<_1XHYQQQQOOTfH3e&MMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%K4v*<<<<<<<<<<<<<<<<<<<<>VAU><<<<<<<<<<_^Xe/><<*k4yOOO2IYoP4Ao3M%MMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%5o;><<<<<<<<<<<<<<<<<<>/zXi_<<<<<<<<<<>VAU><<>/n6CIYhg9KEngBB&MMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMB6s/><<<<<<<<<<<<<<<<<<_lKb;<<<<<<<<<<>(sH^_<<>/6Kas3444aPsXMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%Mwz3r_<<<<<<<<<<<<<<<<<<~TAV_*<<<<<<<<<_OAt*<<<<*UPXPpcXXzWMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM4Pc*<<<<<<<<<<<<<<<<<>;qX!_<<<<<<<<<<<tE7_<<<<<*{i(][email protected]%MMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMncsbe$#mPKI><<<<<<<<<<<<<<<<_^Fe:><<<<<<<<<_^Fe|><<<<*<__{d5gs%MMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMM%Pb6X66XcnsHXI*_<<<<<<<<<<<<<<_0Ay<<<<<<<<<<<>CAV<*<<<>~<]TPEb6MMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMM%MBu4zGtz63E4E5Ey(__>><**<<<<<<<<GAL_<<<<<<<<<>:qK^_>_>>/lY94H66MMMMMMMMMMMMMM
// [email protected]~>)^0GvvvaAnG1):>~~_>>>>>_:zK^_<<<<<<<<<_^Fz[/[email protected]
// MMMMMMMMMMMMMMMMMMMM%m84xT4k*<>__<***]UxEE9oYDj{]]][[]iFa)><<<<<<<<<_U4PenX34EaKPkA$M%MMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMM&qHHnqa5Xj_<<<<<<<>_(DyxEA5E6nnnnnn6Sf*<<<<<<<<<<<o5Ps6EEAaCRMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMM%MWcA9a96HFU_<<<<<<<<<>_*)!DYfpeeeeeea9O_<<<<<<<<<>]ns^)}p449HM%MMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMM%MNKq:|[rr/><<<<<<<<<<<<__>>><<<<<<<*):><<<<<<<<<_7Ek~>~JXE4HRMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMM%MS9Kl_>>_><<<<<<<<<<<<<<<<<<<<<<<<<<><<<<<<<<<<<>TED>*<:cA5ePMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMM%HFh<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>^I)><*_JK4EuwMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMAxXj_*<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>_<<<<>(zAEg&MMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMM8ne]_<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<*t5EX5MMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMWpFb(_<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<~VA49GMMMMMMMMMMMMMMMMMMM
// [email protected]]><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_LXA6bMMMMMMMMMMMMMMMMMMM
// [email protected]}><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_lPAPaMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMM%M3tSG><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_lPAF9MMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMM#nF6^_*<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_JP4FsMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMM%dKd/><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_VEKHdMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMFb4k:><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>)z36PoMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMM4eEv:><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_QEexEWMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMSsEh(_><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_{nXuFdNM%MMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMM%M5a4xD/~><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<_]qSGvEcBM%MMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM3v9EgO|>><*<<<<<<<<<<<<<<<<<<<<<<<<<<<<>>{b5cjHa3M%MMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMAeb54p0)>_><<<<<<<<<<<<<<<<<<<<<<<<<<_;Qn4o7gKHWMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMd6XE4gUr;~_>><<<<<<<<<<<<<<<*<<>>_:7dEA3pnKK%MMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%%KFtGa45XpG7[:<____><<<<<<<<___>*/Dd349HH469%MMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM%MPeFU0tEeeK43PzGLi^]/:<<<<<<:}ijUts3A9CG9h8#&MMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMM%MMM39xOCxEC1IGoxX444Xsgohffffhc934AEAHGl])[email protected]%MMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMM%M%mko4dDTF6Lii!rJOZtoePKKKK33K3F6EnhhF6Oli!{)(7gNMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMEecxKAcDV36Or111rii11lJDOOOOOOO0fAp0UH5Xav01li[]JeNMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMPYnAXxdVQIgAoi111111111riiiiiii!OPFuO2kg6EAsIr11i{;QNMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMRVF3dV2OOQOVP97r1111111111111111rkAcQ2QQ2UysAnDr111i;VMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMM%enFuOOQQ2QQQsX2i111111111111111r1d4uQ2Q2QQOCp4c11111]QMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMM$pEqDDOOOOOO0xK2i111111111111111r1b4Z0OOOOOO0UF90r111[UMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMM%PEskvvZZZZutKnLr1111111111111111ryAayGZZZZuvc3sLr111)2MMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMEsE3KKFPPPKEPGi11111111111111111iLzE3FPPPPX333yr111{}sMMMMMMMMMMMMMMMMMMM




pragma solidity 0.6.4;

/**
 * @dev This abstract contract provides a fallback function that delegates all calls to another contract using the EVM
 * instruction `delegatecall`. We refer to the second contract as the _implementation_ behind the proxy, and it has to
 * be specified by overriding the virtual {_implementation} function.
 * 
 * Additionally, delegation to the implementation can be triggered manually through the {_fallback} function, or to a
 * different contract through the {_delegate} function.
 * 
 * The success and return data of the delegated call will be returned back to the caller of the proxy.
 */
abstract contract Proxy {
    /**
     * @dev Delegates the current call to `implementation`.
     * 
     * This function does not return to its internall call site, it will return directly to the external caller.
     */
    function _delegate(address implementation) internal {
        // solhint-disable-next-line no-inline-assembly
        assembly {
            // Copy msg.data. We take full control of memory in this inline assembly
            // block because it will not return to Solidity code. We overwrite the
            // Solidity scratch pad at memory position 0.
            calldatacopy(0, 0, calldatasize())

            // Call the implementation.
            // out and outsize are 0 because we don't know the size yet.
            let result := delegatecall(gas(), implementation, 0, calldatasize(), 0, 0)

            // Copy the returned data.
            returndatacopy(0, 0, returndatasize())

            switch result
            // delegatecall returns 0 on error.
            case 0 { revert(0, returndatasize()) }
            default { return(0, returndatasize()) }
        }
    }

    /**
     * @dev This is a virtual function that should be overriden so it returns the address to which the fallback function
     * and {_fallback} should delegate.
     */
    function _implementation() internal virtual view returns (address);

    /**
     * @dev Delegates the current call to the address returned by `_implementation()`.
     * 
     * This function does not return to its internall call site, it will return directly to the external caller.
     */
    function _fallback() internal {
        _beforeFallback();
        _delegate(_implementation());
    }

    /**
     * @dev Fallback function that delegates calls to the address returned by `_implementation()`. Will run if no other
     * function in the contract matches the call data.
     */
    fallback () payable external {
        _fallback();
    }

    /**
     * @dev Fallback function that delegates calls to the address returned by `_implementation()`. Will run if call data
     * is empty.
     */
    receive () payable external {
        
        
        
        //_fallback();
    }

    /**
     * @dev Hook that is called before falling back to the implementation. Can happen as part of a manual `_fallback`
     * call, or as part of the Solidity `fallback` or `receive` functions.
     * 
     * If overriden should call `super._beforeFallback()`.
     */
    function _beforeFallback() internal virtual {
    }
}

// File: openzeppelin-solidity/contracts/utils/Address.sol





/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies in extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        // solhint-disable-next-line no-inline-assembly
        assembly { size := extcodesize(account) }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value
        (bool success, ) = recipient.call{ value: amount }("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain`call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
      return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {
        return _functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value, string memory errorMessage) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        return _functionCallWithValue(target, data, value, errorMessage);
    }

    function _functionCallWithValue(address target, bytes memory data, uint256 weiValue, string memory errorMessage) private returns (bytes memory) {
        require(isContract(target), "Address: call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.call{ value: weiValue }(data);
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                // solhint-disable-next-line no-inline-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// File: openzeppelin-solidity/contracts/proxy/UpgradeableProxy.sol







/**
 * @dev This contract implements an upgradeable proxy. It is upgradeable because calls are delegated to an
 * implementation address that can be changed. This address is stored in storage in the location specified by
 * https://eips.ethereum.org/EIPS/eip-1967[EIP1967], so that it doesn't conflict with the storage layout of the
 * implementation behind the proxy.
 * 
 * Upgradeability is only provided internally through {_upgradeTo}. For an externally upgradeable proxy see
 * {TransparentUpgradeableProxy}.
 */
contract UpgradeableProxy is Proxy {
    /**
     * @dev Initializes the upgradeable proxy with an initial implementation specified by `_logic`.
     * 
     * If `_data` is nonempty, it's used as data in a delegate call to `_logic`. This will typically be an encoded
     * function call, and allows initializating the storage of the proxy like a Solidity constructor.
     */
    constructor(address _logic, bytes memory _data) public payable {
        assert(_IMPLEMENTATION_SLOT == bytes32(uint256(keccak256("eip1967.proxy.implementation")) - 1));
        _setImplementation(_logic);
        if(_data.length > 0) {
            // solhint-disable-next-line avoid-low-level-calls
            (bool success,) = _logic.delegatecall(_data);
            require(success);
        }
    }

    /**
     * @dev Emitted when the implementation is upgraded.
     */
    event Upgraded(address indexed implementation);

    /**
     * @dev Storage slot with the address of the current implementation.
     * This is the keccak-256 hash of "eip1967.proxy.implementation" subtracted by 1, and is
     * validated in the constructor.
     */
    bytes32 private constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;

    /**
     * @dev Returns the current implementation address.
     */
    function _implementation() internal override view returns (address impl) {
        bytes32 slot = _IMPLEMENTATION_SLOT;
        // solhint-disable-next-line no-inline-assembly
        assembly {
            impl := sload(slot)
        }
    }

    /**
     * @dev Upgrades the proxy to a new implementation.
     * 
     * Emits an {Upgraded} event.
     */
    function _upgradeTo(address newImplementation) internal {
        _setImplementation(newImplementation);
        emit Upgraded(newImplementation);
    }

    /**
     * @dev Stores a new address in the EIP1967 implementation slot.
     */
    function _setImplementation(address newImplementation) private {
        require(Address.isContract(newImplementation), "UpgradeableProxy: new implementation is not a contract");

        bytes32 slot = _IMPLEMENTATION_SLOT;

        // solhint-disable-next-line no-inline-assembly
        assembly {
            sstore(slot, newImplementation)
        }
    }
}






/**
 * @dev This contract implements a proxy that is upgradeable by an admin.
 * 
 * To avoid https://medium.com/nomic-labs-blog/malicious-backdoors-in-ethereum-proxies-62629adf3357[proxy selector
 * clashing], which can potentially be used in an attack, this contract uses the
 * https://blog.openzeppelin.com/the-transparent-proxy-pattern/[transparent proxy pattern]. This pattern implies two
 * things that go hand in hand:
 * 
 * 1. If any account other than the admin calls the proxy, the call will be forwarded to the implementation, even if
 * that call matches one of the admin functions exposed by the proxy itself.
 * 2. If the admin calls the proxy, it can access the admin functions, but its calls will never be forwarded to the
 * implementation. If the admin tries to call a function on the implementation it will fail with an error that says
 * "admin cannot fallback to proxy target".
 * 
 * These properties mean that the admin account can only be used for admin actions like upgrading the proxy or changing
 * the admin, so it's best if it's a dedicated account that is not used for anything else. This will avoid headaches due
 * to sudden errors when trying to call a function from the proxy implementation.
 * 
 * Our recommendation is for the dedicated account to be an instance of the {ProxyAdmin} contract. If set up this way,
 * you should think of the `ProxyAdmin` instance as the real administrative inerface of your proxy.
 */
contract TransparentUpgradeableProxy is UpgradeableProxy {
    
    
    /**
     * @dev Initializes an upgradeable proxy managed by `_admin`, backed by the implementation at `_logic`, and
     * optionally initialized with `_data` as explained in {UpgradeableProxy-constructor}.
     */
    constructor(address _logic, address _admin, bytes memory _data) public payable UpgradeableProxy(_logic, _data) {
        assert(_ADMIN_SLOT == bytes32(uint256(keccak256("eip1967.proxy.admin")) - 1));
        _setAdmin(_admin);
    }

    /**
     * @dev Emitted when the admin account has changed.
     */
    event AdminChanged(address previousAdmin, address newAdmin);

    /**
     * @dev Storage slot with the admin of the contract.
     * This is the keccak-256 hash of "eip1967.proxy.admin" subtracted by 1, and is
     * validated in the constructor.
     */
    bytes32 private constant _ADMIN_SLOT = 0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103;

    /**
     * @dev Modifier used internally that will delegate the call to the implementation unless the sender is the admin.
     */
    modifier ifAdmin() {
        if (msg.sender == _admin()) {
            _;
        } else {
            _fallback();
        }
    }

    /**
     * @dev Returns the current admin.
     * 
     * NOTE: Only the admin can call this function. See {ProxyAdmin-getProxyAdmin}.
     * 
     * TIP: To get this value clients can read directly from the storage slot shown below (specified by EIP1967) using the
     * https://eth.wiki/json-rpc/API#eth_getstorageat[`eth_getStorageAt`] RPC call.
     * `0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103`
     */
    function admin() external ifAdmin returns (address) {
        return _admin();
    }

    /**
     * @dev Returns the current implementation.
     * 
     * NOTE: Only the admin can call this function. See {ProxyAdmin-getProxyImplementation}.
     * 
     * TIP: To get this value clients can read directly from the storage slot shown below (specified by EIP1967) using the
     * https://eth.wiki/json-rpc/API#eth_getstorageat[`eth_getStorageAt`] RPC call.
     * `0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc`
     */
    function implementation() external ifAdmin returns (address) {
        return _implementation();
    }

    /**
     * @dev Changes the admin of the proxy.
     * 
     * Emits an {AdminChanged} event.
     * 
     * NOTE: Only the admin can call this function. See {ProxyAdmin-changeProxyAdmin}.
     */
    function changeAdmin(address newAdmin) external ifAdmin {
        require(newAdmin != address(0), "TransparentUpgradeableProxy: new admin is the zero address");
        emit AdminChanged(_admin(), newAdmin);
        _setAdmin(newAdmin);
    }

    /**
     * @dev Upgrade the implementation of the proxy.
     * 
     * NOTE: Only the admin can call this function. See {ProxyAdmin-upgrade}.
     */
    function upgradeTo(address newImplementation) external ifAdmin {
        _upgradeTo(newImplementation);
    }

    /**
     * @dev Upgrade the implementation of the proxy, and then call a function from the new implementation as specified
     * by `data`, which should be an encoded function call. This is useful to initialize new storage variables in the
     * proxied contract.
     * 
     * NOTE: Only the admin can call this function. See {ProxyAdmin-upgradeAndCall}.
     */
    function upgradeToAndCall(address newImplementation, bytes calldata data) external payable ifAdmin {
        _upgradeTo(newImplementation);
        // solhint-disable-next-line avoid-low-level-calls
        (bool success,) = newImplementation.delegatecall(data);
        require(success);
    }

    /**
     * @dev Returns the current admin.
     */
    function _admin() internal view returns (address adm) {
        bytes32 slot = _ADMIN_SLOT;
        // solhint-disable-next-line no-inline-assembly
        assembly {
            adm := sload(slot)
        }
    }

    /**
     * @dev Stores a new address in the EIP1967 admin slot.
     */
    function _setAdmin(address newAdmin) private {
        bytes32 slot = _ADMIN_SLOT;

        // solhint-disable-next-line no-inline-assembly
        assembly {
            sstore(slot, newAdmin)
        }
    }

    /**
     * @dev Makes sure the admin cannot access the fallback function. See {Proxy-_beforeFallback}.
     */
    function _beforeFallback() internal override virtual {
        require(msg.sender != _admin(), "TransparentUpgradeableProxy: admin cannot fallback to proxy target");
        super._beforeFallback();
    }
}




contract XXProxy is TransparentUpgradeableProxy {
  
    constructor(address logic, address admin, bytes memory data) TransparentUpgradeableProxy(logic, admin, data) public {

    }

}