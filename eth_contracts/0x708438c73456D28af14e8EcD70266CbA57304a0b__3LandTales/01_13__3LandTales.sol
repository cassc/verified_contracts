// SPDX-License-Identifier: MIT

/// @artist: Imaginary Sina
/// @title: 3LandTales
/// @author: @freshdrops_io

// MMMMMMNk:..xWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// XOkO0k:..  oWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// o. ....ld..oNMMMMMWXKKKXNWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// x. :kxOWO,..'ckOd:,.....':oONMMMWNXXXXXXXXXXNMMMMMMMMMMMMMMMMMMMMMMWWWNNNXXKKKXWMMMWNXXXXXXXXXXXXXXNWNKKKXXXXNNNNWMMWWWWWWNNNWWMMMMMMMMMMMMMMMMMMMMMMM
// 0' :XNXXXO:.     .':cllc;. .,dXNd'..........lXMMMMMMMMMMMMMKdolcc::;;,''......,0MMWd'..............:Oo..........'cKO:;;,,'''',,:cox0NWMMMMMMMMMMMMMMMM
// c .:ooOXo..    'o0NWMMMMWXkc. ,kc  'ccccl;. ;KMMMMMMMMMMMMNl  ...',,;;::ccc:.  oWMNc .;ccclllllll;. :; .coolll:. 'kl .;ccllllcc:,'..':dKWMMMMMMMMMMMMM
// . ... .;..'. .lKMMMMMMMMMMMWO' .'  oWMMMMK, ,KMMMMMMMMMMMM0' ;0XNNWWWWMMMMWNo  ,KMNc .OMMMMMMMMMMXc    '0MMMMMX; 'kc ,KMMMMMMMMMWNKkl'..lKWMMMMMMMMMMM
// 0O0XKc  .:'  oNMMMMWXKKWMMMMMO'    lWMMMMK; '0MMMMMMMMMMMWo .xWMMMMMMMMMMMMM0' .xWNc .OMMMMMMMMMMMK,   '0MMMMMX; 'x; :XMMMMMMMMMMMMMMXx' .xNMMMMMMMMMM
// MMMMMXdlOd. 'OMMMMKc...;xNMMMWd.   cNMMMMX; '0MMMMMMMMMMMK; '0MMMMMMMMMMMMMMNl  :XNl .OMMMMMMMMMMMMk.  ,KMMMMMX; ,x, cNMMMMMMMMMMMMMMMMXc .xWMMMMMMMMM
// MMMMMMMMMk. '0MMMWo  .. .kMMMMk.   ;XMMMMX: .OMMMMMMMMMMMx. cNMMMMWOcckWMMMMMk. .ONl .kMMMMMMMMMMMMNo  ,KMMMMMX; ,d' lWMMMMMMXXNMMMMMMMMXc 'OMMMMMMMMM
// MMMMMMMMMK; .kMMMWd. '. .xMMMWd.   '0MMMMNc .OMMMMMMMMMMN: .xMMMMMX:  ,0MMMMMX:  lXl .kMMMMMMNNWMMMMX: ,KMMMMMX; ,l. oWMMMMMNl.,oKMMMMMMMO. lWMMMMMMMM
// MMMMMMMMMWd. ,ooc:'   .,xNMMM0,    .OMMMMWl .kMMMMMMMMMMO. ,0MMMMMO.  .xMMMMMWx. 'Ol .xMMMMMMOcOMMMMM0,;KMMMMMX; ,c..xMMMMMMK;   ;KMMMMMMK, ;XMMMMMMMM
// MMMMMMMMMMXl...','  .o0NMMMWO,  ;' .xMMMMWl .xMMMMMMMMMWl  cNMMMMMKdllo0MMMMMMX: .oc .xMMMMMMO.;XMMMMWxlKMMMMMX; ,: .kMMMMMMO. . .xMMMMMM0' ;KMMMMMMMM
// MMMMMMMMMMMNKKXNWK; '0MMMMMXc.  '.  oWMMMWo .xMMMMMMMMM0, .xWMMMMMMMMMMMMMMMMMMx. ,;  dWMMMMMO..dWMMMMWNWMMMMMX; ,; .OMMMMMWd.   'OMMMMMMk. lNMMMMMMMM
// MMMMMMMMMMMMMMMMMWl .l00XWMMNOc.    lNMMMWd..dWWWWWWWNNo. '0MMMMMMMMMMMMMMMMMMMX; ..  oWMMMMMO' '0MMMMMMMMMMMMX; ', ,0MMMMMNc  .;kWMMMMMX: .kMMMMMMMMM
// MMMMMMMMMMMWNX0kxo,   ...:OWMMWk.   cNMMMMx. .;;;;,,''..  cNMMMMMXxlllloKMMMMMMWd.    lWMMMMM0'  :XMMMMMMMMMMMX; '. ;XMMMMMWOdx0NMMMMMMNo. cXMMMMMMMMM
// MMMMMMMMMMXl'......   ;;  :XMMMNl   ;XMMMMk'...'',,,;,.  .kMMMMMWd. ..  lWMMMMMMK,    lNMMMMM0'  .dWMMMMMMMMMMK; .. :NMMMMMMMMMMMMMMMMXl. ;KMMMMMMMMMM
// MMMMMMMMMMK;  ;k0KKl. '' .dNMMMWo   ,KMMMMWXXXNNWWWWWX:  ,KMMMMMK; ;OO' ;KMMMMMMWo    cNMMMMM0'   .kWMMMMMMMMMK, .. cNMMMMMMMMMMMMMMNx, .lXMMMMMMMMMMM
// MMMMMMMMMMWd. ;KMMMNkl:coOWMMMM0,   'OMMMMMMMMMMMMMMMX:  cNMMMMWd..dWNl .kMMMMWNKo.   cNMMMMM0' ;, ,0WWMMMMMMMK, .  oWMMMMMMMMMMWKko,..c0WMMMMMMMMMMMM
// MMMMMMMMMMMNl. :0WMMMMMMMMMMMWO, .. .kWWWWWWWWWNNNXXKO,  'clodxd, '0MMx. ,oolc;'.     .:cc:::,. lx. .;;;::::::;.    ,oddddooolc:,...;dKWMMMMMMMMMMMMMM
// MMMMMMMMMMMMNx' .cxOKXNWWNX0d;..c0o. .;;;;;;,,,'''....   ....     ;0XXk.    ..,:ldkx:;;;;;;;;'  .,.          .,;;;;;;,,,'.     .':dKWMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMWXx:.....',,'...'cOXXx'..   .','''''.......';:oOOd.  .....    .lXWMMMMMWWWWWWNOc.  .,:clool:'.  .c0WWWWN0o:..........':dKWMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMWWNXOd:,..     .,;,'...     .'..............  '0MK; .;lllll'  .oWMMMMMMMMMMWKc. 'oOXWWMMMMMWNKx:. .kWW0c. .cdOKXXXX0xc. .lXMMMMMMMMMMMMMM
// MMMMMMMMMMMWO:,'....      ....',;:clloo:   .:odxxkkOO0KKKl  oWX: '0MMMMWd  .oWMMMMMMMMMWO' .dXMMMMMMMMMMMMMMWk' ,Ox. 'dXMMMMMMMMMMWXl. :KMMMMMMMMMMMMM
// MMMMMMMMMMMWd. .:cclodxkO00KXNWWWMMMMMMO.  :XMMMMMMMMMMMM0' ,KNc .OMMMMMd. .lWMMMMMMMMM0, .xWMMMWKxoodkKWMMMMWd. .. ,0MMMMMMWNNWMMMMNl  oWMMMMMMMMMMMM
// MMMMMMMMMMMMO. cNMMMMMMMMMMMMMMMMMMMMMM0' .xWMMMMMMMMMMMMNl .dNo .kMMMMMx. .lWMMMMMMMMNl  lNMMMXo.  .  .oXMMMMO.   .xMMMMMM0c'.:0MMMMO. :XMMMMMMMMMMMM
// MMMMMMMMMMMMK, ,KMMMMMMMMMMMMMMMMMMMWNNO, '0MMMMMN0OKWMMMMk. ;Kd..dMMMMMk.  lNMMMMMMMMK, .kMMMMx. .o0k, .lKXXXx.   .xMMMMMNl    ,k000x' ,KMMMMMMMMMMMM
// MMMMMMMMMMMMN: .kMMMWWNNWMMMMMMKdlc:,'..  cNMMMMWd. ,0MMMMX: .xx. lWMMMMO.  cNMMMMMMMMX; .kMMMMO'  'cc.   .....  .  ;KMMMMW0:.    ....  :XMMMMMMMMMMMM
// MMMMMMMMMMMMWo  ,lc:;,''dWMMMMMk.  .';;. .xMMMMMX:  .dWMMMWx. :d. cNMMMM0'  cNMMMMMMMMWo  oWMMMW0l;'...  ,dxxxxxxOo. ,kNMMMMW0o,.  .cdxkKWMMMMMMMMMMMM
// MMMMMMMMMMMMMk'..',;:,  :XMMMMM0' .xWW0, ,0MMMMM0:..'dWMMMMX; .c' ;XMMMM0'  :NMMMMMMMMMK; .xWMMMMMWNNXo. lWMMMMMMMWO:..,o0NMMMMNKd;..:OWMMMMMMMMMMMMMM
// MMMMMMMMMMMMMWXKXNWWM0' ,KMMMMMK; .xMWx. cNMMMMMWNXXXNMMMMMWx. '. ,KMMMMK,  :XMMMMMMMMMMK: .:kNMMMMMMMx. lNMMMMMMMMMW0o,..,lONMMMMNO:..oNMMMWNXKKNWMMM
// MMMMMMMMMMMMMMMMMMMMMX; '0MMMMMNc  oWNc .dWMMMMMMMMMMMMMMMMMK;    .OMMMMK,  ;XMMMMMMMMMMWd. 'xNMMMWKkd:  ,xOOOOO00d::cc:'    ,xNMMMMNd. lXKo;'....;xXM
// MMMMMMMMMMMMMMMMMMMMMNl .kMMMMMWo  cN0' '0MMMMMMNKKK0KNMMMMMWd.   .kMMMMX;  .coollllccclc. cXMMMWk;...    ..... .;.  .,''..    :KMMMMWo..,. .:odo:. ;0
// MMMMMMMMMMMMMMMMMMMMMWd. oWMMMMMk. ;Kd. lNMMMMMK;.....oNMMMMM0,   .xMMMMNl....',,;;;;'    .OMMMM0' .lo. .d0000c .,. ,0WNNNk.   .kMMMMMO.   'OWNKNWO' :
// MMMMMMMMMMMMMMMMMMMMMMO. :NMMMMM0' 'x: .kMMMMMWo  ,:. ,KMMMMMWo    oWMMMMNXXXNNWWWWWWx.   'OMMMMXc. .. .dNMMMWo .:' .OMMMMWOl::xNMMMMMx.   ,OXK0NM0' ;
// MMMMMMMMMMMMMMMMMMMMMMK, ,KMMMMMK; .:. ,KMMMMM0, 'ONc .kMMMMMMO.   lNMMMMMMMMMMMMMMMMk. .  lXMMMMNOdooxKWMMMM0, .kd. ;KWMMMMMMMMMMMMM0,   .:k000ko' 'k
// MMMMMMMMMMMMMMMMMMMMMMNc .kMMMMMNc  .  ,kKKXNNo. lNMk. cKK0kdl;.   :XWWWWWWWWWNNNXXKKo..ll. ;OWMMMMMMMMMMMMNx' .oNNx. .o0NWMMMMMMMN0o.   .dWWNWW0, ,0W
// MMMMMMMMMMMMMMMMMMMMMMWo  oWMWWWXl       ...''. .OMMX;  .....';c:. .,;;,,,,,,''''..... .xNO:..;d0NWMMMMWN0d,  'xNMMWKo,..';clooolc,. .:, .xWN0XMX: ;XM
// MMMMMMMMMMMMMMMMMMMMMMMk. .::;,,'.  ,xkxolc:;,''oNMMWOcloxO0XNWMNklllllloooooodddxxxxkkONMMNOl'...;:cc:;'. .;dXMMMMMMMN0xl;'.....';lxKWK: .lk00k:..oWM
// MMMMMMMMMMMMMMMMMMMMMMM0, ....',;:clOWMMMMMMWWNNWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMXOdc:;,,,;:lxKWMMMMMMMMMMMMMWNXKKKXNWMMMMMNx:'.....;xNMM
// MMMMMMMMMMMMMMMMMMMMMMMNOdkO0KXNWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWWNWWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWKkddk0NMMMM

pragma solidity ^0.8.4;

import "@openzeppelin/contracts/token/ERC1155/ERC1155.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC1155/extensions/ERC1155Supply.sol";
import "@openzeppelin/contracts/utils/cryptography/MerkleProof.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";

contract _3LandTales is ERC1155, Ownable, ERC1155Supply, ReentrancyGuard {
    string public name;
    string public symbol;

    mapping(uint256 => bytes32) public merkleRoot;
    mapping(uint256 => uint256) public maxSupply;
    mapping(uint256 => uint256) public costInWei;
    mapping(uint256 => bool) public allowlistSaleEnabled;
    mapping(uint256 => bool) public publicSaleEnabled;
    mapping(address => uint256) public addressMinted;

    constructor(
        string memory _name,
        string memory _symbol,
        string memory _uri
    ) ERC1155(_uri) {
        name = _name;
        symbol = _symbol;
    }

    modifier mintCompliance(uint256 _tokenId) {
        require(
            (addressMinted[_msgSender()] & (1 << _tokenId)) == 0,
            "Wallet already minted"
        );
        require(
            totalSupply(_tokenId) < maxSupply[_tokenId],
            "Max supply reached"
        );
        require(msg.value == costInWei[_tokenId], "Incorrect amount of ether");
        _;
    }

    function setURI(string memory _uri) public onlyOwner {
        _setURI(_uri);
    }

    function setCost(uint256 _tokenId, uint256 _cost) public onlyOwner {
        costInWei[_tokenId] = _cost;
    }

    function setMaxSupply(uint256 _tokenId, uint256 _supply) public onlyOwner {
        maxSupply[_tokenId] = _supply;
    }

    function setAllowlistSaleEnabled(uint256 _tokenId, bool _enabled)
        public
        onlyOwner
    {
        allowlistSaleEnabled[_tokenId] = _enabled;
    }

    function setPublicSaleEnabled(uint256 _tokenId, bool _enabled)
        public
        onlyOwner
    {
        publicSaleEnabled[_tokenId] = _enabled;
    }

    function setMerkleRoot(uint256 _tokenId, bytes32 _merkleRoot)
        public
        onlyOwner
    {
        merkleRoot[_tokenId] = _merkleRoot;
    }
    
    function mint(
        address _to,
        uint256 _id,
        uint256 _amount
    ) external onlyOwner {
        _mint(_to, _id, _amount, "");
    }

    function mintBatch(
        address _to,
        uint256[] memory _ids,
        uint256[] memory _amounts
    ) external onlyOwner {
        _mintBatch(_to, _ids, _amounts, "");
    }

    function mint(uint256 _tokenId) public payable mintCompliance(_tokenId) {
        require(publicSaleEnabled[_tokenId], "Public sale not active");

        addressMinted[_msgSender()] |= 1 << _tokenId;
        _mint(_msgSender(), _tokenId, 1, "");
    }

    function mintWithAllow(uint256 _tokenId, bytes32[] calldata _merkleProof)
        public
        payable
        mintCompliance(_tokenId)
    {
        require(allowlistSaleEnabled[_tokenId], "Allowlist sale not active");
        bytes32 leaf = keccak256(abi.encodePacked(_msgSender()));
        require(
            MerkleProof.verify(_merkleProof, merkleRoot[_tokenId], leaf),
            "Invalid proof!"
        );

        addressMinted[_msgSender()] |= 1 << _tokenId;
        _mint(_msgSender(), _tokenId, 1, "");
    }

    function _beforeTokenTransfer(
        address _operator,
        address _from,
        address _to,
        uint256[] memory _ids,
        uint256[] memory _amounts,
        bytes memory _data
    ) internal override(ERC1155, ERC1155Supply) {
        super._beforeTokenTransfer(
            _operator,
            _from,
            _to,
            _ids,
            _amounts,
            _data
        );
    }

    function withdraw() public onlyOwner nonReentrant {
        uint256 balance = address(this).balance;
        require(balance > 0, "No ether left to withdraw");

        (bool success, ) = (_msgSender()).call{value: balance}("");
        require(success, "Transfer failed.");
    }
}